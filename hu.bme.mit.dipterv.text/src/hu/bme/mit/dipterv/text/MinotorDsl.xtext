grammar hu.bme.mit.dipterv.text.MinotorDsl with org.eclipse.xtext.common.Terminals

generate minotorDsl "http://www.bme.hu/mit/dipterv/text/MinotorDsl"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Domain:
	(specification='specification')? (name=ID)? ('{')?
	objects+=Object*
	parameters+=Parameter*
	clocks+=Clock*
	constraints+=Constraint*
	scenarios+=Scenario* ('}')?
;

AttributeValue:
	value=STRING | value=REAL | value=NUMBER | value='true' | value='false'
;

Scenario:
	'scenario' name=ID '{'
	scenariocontents+=ScenarioContent*
	'}'
;

ScenarioContent:
	alt+=Alt | message+=Message | par+=Par | loop+=Loop | paramConstraint+=ParameterConstraint

;

Message:
	LooseMessage | StrictMessage | PastMessage | FutureMessage | StrictFutureMessage
	| RequiredLooseMessage | RequiredStrictMessage | RequiredPastMessage | RequiredFutureMessage | RequiredStrictFutureMessage
	| FailMessage | FailStrictMessage | FailPastMessage
;

LooseMessage:
	'message' name=ID '(' (params+=Params | constantparams+=ConstantParams) ')'
	sender=[Object] '->' receiver=[Object]
	('clockConstraint' '{' cConstraint=ClockConstraintExpression '}')? 
	(resetclock=ResetClock)? ';'
;

StrictMessage:
	'strict'
	message+=LooseMessage
;

PastMessage:
	'pastConstraint'
	'{' c=[Constraint] (',' constraintexp=ClockConstraintExpression)? (',' resetinconstraint=ResetClock)? '}'
	message+=LooseMessage
;

FutureMessage:
	'futureConstraint'
	'{' c=[Constraint] (',' constraintexp=ClockConstraintExpression)? (',' resetinconstraint=ResetClock)? '}'
	 message+=LooseMessage
;

StrictFutureMessage:
	'strict'
	futureMessage+=FutureMessage
;

RequiredLooseMessage:
	'required'
	message+=LooseMessage
;

RequiredStrictMessage:
	'required'
	strictMessage+=StrictMessage
;

RequiredPastMessage:
	'required'
	pastMessage+=PastMessage
;

RequiredFutureMessage:
	'required'
	futureMessage+=FutureMessage
;

RequiredStrictFutureMessage:
	'required'
	strictFutureMessage+=StrictFutureMessage
;

FailMessage:
	'fail'
	message+=LooseMessage
;

FailStrictMessage:
	'fail'
	strictMessage+=StrictMessage
;

FailPastMessage:
	'fail'
	pastMessage+=PastMessage
;

ResetClock:
	'reset' clock=[Clock]
;

ClockConstraint:
	GreaterClockConstraint | SmallerClockConstraint | GreaterEqualClockConstraint | SmallerEqualClockConstraint
;

GreaterClockConstraint:
	'>' '(' clock=[Clock] ',' constant=NUMBER ')'
;

SmallerClockConstraint:
	'<' '(' clock=[Clock] ',' constant=NUMBER ')'
;

GreaterEqualClockConstraint:
	'>=' '(' clock=[Clock] ',' constant=NUMBER ')'
;

SmallerEqualClockConstraint:
	'<=' '(' clock=[Clock] ',' constant=NUMBER ')'
;

ClockConstraintExpression:
	  ClockConstraint | NotClockConstraintExpression | AndClockConstraintExpression
;

NotClockConstraintExpression:
	'not' '(' notClockConstraint=ClockConstraint ')'
;

AndClockConstraintExpression:
	lclockconstraint=ClockConstraint 'and' rclockconstraint=ClockConstraint
;

Params:
	 (params+=[Parameter])? (',' params+=[Parameter])* 
;

ConstantParams:
	(values+=AttributeValue)? (',' values+=AttributeValue)* 
;

Parameter:
	type=Type name=ID ('=')? (value=AttributeValue)? ';'
;

ParameterConstraint:
	'assertParameter' '[' param=[Parameter] operator+=Operator value+=AttributeValue ']' 'in' object=[Object] ';'
;

Operator:
	greater?='>' | smaller?='<' | greaterequals?='>=' | smallerequals?='<=' | equals?='==' | notequals?='!='
;

Clock:
	'clock' name=ID ';'
;

enum Type:
	integer | float | bool | string
;

terminal NUMBER:
	('0'..'9')*
;

terminal REAL:
	('0'..'9')* '.' ('0'..'9')*
;

ObjectType:
	'object' name=ID
;

Object:
	object+=ObjectType name=ID ';'
;

Constraint:
	'constraint' name=ID '{'
		messages+=Message* 
	'}'
;

Alt:
	'alt' expressions+=Expression*
;

Expression:
	'(' altCondition=LogicalExpression ')' '{'
		messages+=Message*
	'}'
;

LogicalExpression:
	UnaryLogicalExpression | BinaryLogicalExpression
;

BinaryLogicalExpression:
	EqualsExpression | EqualsBooleanExpression | GreaterThanExpression | LesserThanExpression |
	AndExpression | OrExpression
;

AndExpression:
	'and' '(' lhs=LogicalExpression ',' rhs=LogicalExpression ')'
;

OrExpression:
	'or' '(' lhs=LogicalExpression ',' rhs=LogicalExpression ')'
;

EqualsExpression:
	'equals' '(' lhs=[Parameter] ',' rhs=NUMBER ')'
;

EqualsBooleanExpression:
	'equals' '(' lhs=[Parameter] ',' rhs='true' ')' | 'equals' '(' lhs=[Parameter] ',' rhs='false' ')'
;

GreaterThanExpression:
	'greater' '(' lhs=[Parameter] ',' rhs=NUMBER ')'
;

LesserThanExpression:
	'lesser' '(' lhs=[Parameter] ',' rhs=NUMBER ')'
;

UnaryLogicalExpression:
	NotLogicalExpression
;

NotLogicalExpression:
	'not' '(' operand=LogicalExpression ')'
;

Par:
	'par' '{' parexpression+=ParExpression* '}'
;

ParExpression:
	'case' name=ID '{' messages+=Message* '}'
;

Loop:
	'loop' '(' min=NUMBER ',' max=NUMBER ')' '{' 
		messages+=Message*
	'}'
;